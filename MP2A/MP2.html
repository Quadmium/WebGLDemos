<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>MP2</title>
<meta charset="utf-8"> 
</head>

    
<script id="shader-vs" type="x-shader/x-vertex">
attribute vec3 aVertexNormal;
attribute vec3 aVertexNormalNext;
attribute vec3 aVertexPosition;
attribute vec3 aVertexPositionNext;
attribute vec3 aVertexColor;
attribute vec3 aVertexColorNext;

uniform float uLerpNext;

uniform mat4 uMVMatrix;
uniform mat4 uPMatrix;
uniform mat3 uNMatrix;

uniform vec3 uLightPosition;
uniform vec3 uAmbientLightColor;
uniform vec3 uDiffuseLightColor;
uniform vec3 uSpecularLightColor;

uniform vec3 uKAmbient;
uniform vec3 uKDiffuse;
uniform vec3 uKSpecular;
uniform float uShininess;

varying vec4 vColor;

void main(void) {
    // Lerp between current model and next model
    vec3 vPos = mix(aVertexPosition, aVertexPositionNext, uLerpNext);
    vec3 vCol = mix(aVertexColor, aVertexColorNext, uLerpNext);
    vec3 vNorm = normalize(mix(aVertexNormal, aVertexNormalNext, uLerpNext));

    // Get the vertex position in eye coordinates
    vec4 vertexPositionEye4 = uMVMatrix * vec4(vPos, 1.0);
    vec3 vertexPositionEye3 = vertexPositionEye4.xyz / vertexPositionEye4.w;
  
    // Calculate the vector (l) to the light source
    vec3 vectorToLightSource = normalize(uLightPosition - vertexPositionEye3);
    
    // Transform the normal (n) to eye coordinates
    vec3 normalEye = normalize(uNMatrix * vNorm);
    
    // Calculate n dot l for diffuse lighting
    float diffuseLightWeightning = max(dot(normalEye, 
                                    vectorToLightSource), 0.0);
    
    // The camera in eye coordinates is located in the origin and is pointing
    // along the negative z-axis. Calculate viewVector (v) 
    // in eye coordinates as:
    // (0.0, 0.0, 0.0) - vertexPositionEye3
    vec3 viewVectorEye = -normalize(vertexPositionEye3);
    
    // Calculate H for specular
    vec3 halfway = normalize(vectorToLightSource + viewVectorEye);

    // Calculate N * H
    float ndoth = max(dot(normalEye, halfway), 0.0);
    
    // Calculate (N * H) ^ a
    float specularLightWeightning = pow(ndoth, uShininess);

    // Sum up all three reflection components and send to the fragment shader
    vColor = vec4(((uAmbientLightColor*uKAmbient)
                + (uDiffuseLightColor*vCol) * diffuseLightWeightning
                + ((uSpecularLightColor*uKSpecular) * specularLightWeightning)),1.0);
    gl_Position = uPMatrix*uMVMatrix*vec4(vPos, 1.0);
}
</script>
  
<script id="shader-fs" type="x-shader/x-fragment">
precision mediump float;
varying vec4 vColor;
void main(void) {
        gl_FragColor = vColor;
 }
</script>

<script src="mangled.js"></script>   
<body onload="startup();">
 <canvas id="myGLCanvas" width="800" height="800"></canvas>
    <form id="input_form">
        <fieldset>
            <legend>Rendering Parameters</legend>
         <input type="radio" name="primitive" id="wireframe" value="wireframe" checked> Wireframe
         <input type="radio" name="primitive" id="polygon" id="polygon" value="polygon"> Polygon
        </fieldset>
        </form>
</body>

</html>
